import org.apache.tools.ant.filters.ReplaceTokens

buildscript {
    repositories {
        mavenCentral()
        maven { url = "http://files.minecraftforge.net/maven" }
        maven { url = "https://oss.sonatype.org/content/repositories/snapshots/" }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.1-SNAPSHOT'
    }
}

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'maven'
apply from: 'build.properties'

minecraft {
    runDir = "eclipse/"
}

project.version = getVersion()

minecraft {	
    replaceIn "Reference.java"
    replace "@VERSION@", project.version
    replace "@MODID@", project.archivesBaseName
    replace "@MODNAME@", project.longname
}

tasks["jar"].classifier = getArtifactID()

task deobfJar(type: Jar) {
    from sourceSets.main.output
    classifier = getArtifactID() + '-deobf'
}

tasks["sourceJar"].classifier = getArtifactID() + '-sources'

tasks.build.dependsOn('deobfJar', 'sourceJar')

processResources
{
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand project.properties
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

repositories {
    mavenLocal()
}

artifacts {
    archives jar
    archives deobfJar
    archives sourceJar
}

install.dependsOn build

def getVersion() {
    def stdout = new ByteArrayOutputStream()
    exec {
        executable "git"
        args "rev-list", "--count", "HEAD"
        standardOutput = stdout
    }
    def revision = stdout.toString().trim()
    return "${major}.${minor}.${patch}.${revision}"
}

def getArtifactID() {
    def stdout = new ByteArrayOutputStream()
    exec {
        executable "git"
        args "rev-parse", "--abbrev-ref", "HEAD"
        standardOutput = stdout
    }
    def branch = stdout.toString().trim()
    if(branch == "HEAD") {
        stdout.reset()
        exec {
            executable "git"
            args "rev-parse", "--short", "HEAD"
            standardOutput = stdout
        }
        branch = stdout.toString().trim()
    }
	return "${branch == 'master' ? '' : branch + '-'}${minecraft.version}"
}

