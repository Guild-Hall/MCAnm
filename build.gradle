import org.apache.tools.ant.filters.ReplaceTokens

buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
    }
}

apply plugin: 'forge'
apply plugin: 'maven'
apply from: 'build.properties'

sourceCompatibility = 1.8
targetCompatibility = 1.8

minecraft {
    version = "1.7.10-10.13.3.1380-1.7.10"
    runDir = "eclipse/"

    replaceIn "Reference.java"
    replace "@VERSION@", project.version
    replace "@MODID@", project.archivesBaseName
    replace "@MODNAME@", project.longname
}

task deobfJar(type: Jar) {
    from sourceSets.main.output
    classifier = 'deobf'
}

task sourceJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

tasks.build.dependsOn('deobfJar', 'sourceJar')

processResources
{
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        filter(ReplaceTokens, tokens:
                [VERSION: project.version,
                 MCVERSION: project.minecraft.version,
                 MODID: project.archivesBaseName,
                 MODNAME: project.longname])
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

repositories {
    mavenLocal()
}

artifacts {
    archives jar
    archives deobfJar
    archives sourceJar
}

install.dependsOn build

dependencies {}
